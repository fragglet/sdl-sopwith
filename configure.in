AC_INIT(SDL Sopwith, 1.8.x, fraggle@gmail.com, sdl_sopwith)
AC_CONFIG_AUX_DIR(autotools)

dnl optimisations

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(AC_PACKAGE_TARNAME,AC_PACKAGE_VERSION)

dnl check for cc

AC_PROG_CC
AC_PROG_RANLIB

AC_ARG_ENABLE(psion, [  --enable-psion	  Compile for Psion Linux], [
	psiprefix=arm-uclibc

	echo "*** psion hack: using $psiprefix as prefix for compile utils"

	target=$psiprefix
	CC="${psiprefix}-gcc"
	LD="${psiprefix}-ld"
	AR="${psiprefix}-ar"
	NM="${psiprefix}-nm"
	RANLIB="${psiprefix}-ranlib"
	SOPWITH_BINS=psopwith
	SOPWITH_SUBDIRS=psion
	CFLAGS="$CFLAGS -DPSION"
])

dnl network libraries

case "$target" in
    *-*-cygwin* | *-*-mingw32*)
	LDFLAGS="$LDFLAGS -lwsock32"
	;;
    sparc*-*-solaris*)
	LDFLAGS="$LDFLAGS -lsocket -lnsl"
	;;
    *-*-qnx*)
	LDFLAGS="$LDFLAGS -lsocket"
	;;
esac

AM_CONFIG_HEADER(config.h:config.hin)

dnl install programs

AC_PROG_INSTALL

dnl libraries

AM_PATH_SDL(1.1.3, [
	echo "Building SDL version."
	SOPWITH_BINS="$SOPWITH_BINS sopwith"
	SOPWITH_SUBDIRS="$SOPWITH_SUBDIRS sdl"
],[
	echo "Not building SDL version."
])

PKG_CHECK_MODULES(GTK, glib-2.0 >= 2.0 gtk+-2.0 >= 2.0, [
	echo "Building Gtk+ version."
	SOPWITH_BINS="$SOPWITH_BINS gtksopwith"
	SOPWITH_SUBDIRS="$SOPWITH_SUBDIRS gtk"
],[
	echo "Not building Gtk+ version."
])

AC_SUBST(SOPWITH_BINS)
AC_SUBST(SOPWITH_SUBDIRS)

dnl check for TCPIP support

AC_CHECK_HEADER(netinet/ip.h, [
    AC_DEFINE(TCPIP, 1, [TCPIP multiplayer support])
],,)

AC_SUBST(ac_aux_dir)

dnl generate Makefiles

AC_OUTPUT([
Makefile
src/Makefile
src/gtk/Makefile
src/psion/Makefile
src/sdl/Makefile
doc/Makefile
])

